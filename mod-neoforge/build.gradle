import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
    id 'net.darkhax.curseforgegradle'
    id 'com.modrinth.minotaur'
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':mod-common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        server {
            server()
        }

        List<String> dataArgs = List.of(
                "--mod",
                mod_id.toString(),
                "--all",
                "--output",
                project(':mod-common').file("src/generated/resources/").getAbsolutePath(),
                "--existing",
                project(':mod-common').file("src/main/resources/").getAbsolutePath()
        )

        data {
            programArguments.addAll(dataArgs)
            clientData()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

dependencies {
    implementation 'com.github.glitchfiend:TerraBlender-neoforge:' + minecraft_version + '-' + terrablender_version
    implementation "software.bernie.geckolib:geckolib-neoforge-${minecraft_version}:${geckolib_version}"
    implementation "com.teamresourceful.resourcefulconfig:resourcefulconfig-neoforge-${minecraft_version}:$resourcefulconfig_version"
}

sourceSets.main.resources { srcDir project.project(':mod-common').file('src/generated/resources/').getAbsolutePath() }

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    apiToken = rootProject.findProperty('curseforge_token')
    debugMode = false

    var mainFile = upload(curseforge_project, tasks.jar)
    mainFile.changelog = rootProject.file(mod_changelog_file)
    mainFile.changelogType = 'markdown'
    mainFile.displayName = "$mod_name" + ' ' + "$version" + ' - NeoForge'
    mainFile.releaseType = 'release'
    mainFile.addRequirement('terrablender-neoforge')
    mainFile.addRequirement('geckolib')
    mainFile.addRequirement('resourceful-config')
    mainFile.addModLoader('NeoForge')
}

modrinth {
    token = rootProject.findProperty('modrinth_token').toString()
    debugMode = false

    projectId = modrinth_project
    versionName = "$mod_name" + ' ' + "$version" + ' - NeoForge'
    changelog = rootProject.file(mod_changelog_file).getText()
    uploadFile = jar
    versionType = 'release'
    gameVersions = [minecraft_version]
    loaders = ['neoforge']
    dependencies {
        required.project 'fabric-api'
        required.project 'terrablender'
        required.project 'geckolib'
        required.project 'resourceful-config'
    }
}