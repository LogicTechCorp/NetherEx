import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
    id 'net.darkhax.curseforgegradle'
    id 'com.modrinth.minotaur'
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"

    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modImplementation "com.github.glitchfiend:TerraBlender-fabric:${minecraft_version}-${terrablender_version}"
    modImplementation "software.bernie.geckolib:geckolib-fabric-${minecraft_version}:${geckolib_version}"
    modImplementation "com.teamresourceful.resourcefulconfig:resourcefulconfig-fabric-${minecraft_version}:$resourcefulconfig_version"
}

loom {
    def aw = project(':mod-common').file("src/main/resources/${mod_id}.accesswidener")

    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }

    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }

    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }

        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}

sourceSets.main.resources { srcDir project.project(':mod-common').file('src/generated/resources/').getAbsolutePath() }

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    apiToken = rootProject.findProperty('curseforge_token')
    debugMode = false

    var mainFile = upload(curseforge_project, tasks.remapJar)
    mainFile.changelog = rootProject.file(mod_changelog_file)
    mainFile.changelogType = 'markdown'
    mainFile.displayName = "$mod_name" + ' ' + "$version" + ' - Fabric'
    mainFile.releaseType = 'release'
    mainFile.addRequirement('fabric-api')
    mainFile.addRequirement('terrablender-fabric')
    mainFile.addRequirement('geckolib')
    mainFile.addRequirement('resourceful-config')
    mainFile.addModLoader('Fabric')
}

modrinth {
    token = rootProject.findProperty('modrinth_token').toString()
    debugMode = false

    projectId = modrinth_project
    versionName = "$mod_name" + ' ' + "$version" + ' - Fabric'
    changelog = rootProject.file(mod_changelog_file).getText()
    uploadFile = remapJar
    versionType = 'release'
    gameVersions = [minecraft_version]
    loaders = ['fabric']
    dependencies {
        required.project 'fabric-api'
        required.project 'terrablender'
        required.project 'geckolib'
        required.project 'resourceful-config'
    }
}